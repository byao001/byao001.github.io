---
title: "IO Monad"
categories: functional_programming
---
# IO Monad
------------------------

Programmers deal with I/O all the time, read files, write to terminal, write to sockets, etc. When programers talking about I/O, it usually means data is transfered from one place to some other place. For example, when we write a sentence to a file, we transfer the string from main memory to disk, or when we write to terminal, we copy data to the standard output's buffer. In both cases, the contents in files or the buffer has changed, which means there is side-effect.

When we say a function is pure, we mean the function does not have side-effect. In most of the programming languages, I/O is applied through some funtion calls, like `read_line()`, `write_string()`, etc.

At the first time I learnt **Haskell**, all the books and tutorials told me that Haskell is a pure functional programming language, which means all the expressions in Haskell are pure, including the I/O functional calls. But how is it possible that a I/O function call is pure when it writes "Hello, World!" to the standard output? This problem confused me for a very long time.

It is impossible that an I/O function call is pure, if this function call actually writes "Hello, World!" to standard output. Because this against the definition of "pure". Then what does the I/O functions in Haskell actually do? Why we say those functions are pure?

First, let's see some code, open ghci and type:

{% highlight haskell %}
Prelude> let main () = putStrLn "Hello"
Prelude> :t main
main :: () -> IO ()
Prelude> main ()
Hello
{% endhighlight %}

The `main` function "write" something to the standard output when it is called. A weird thing is the type of `main`, `IO -> IO ()`. What does `IO ()` mean? If we write similar code in OCaml, which is another functional programming language:

{% highlight ocaml %}
utop # let main () = print_endline "Hello";;
val main : unit -> unit = <fun>

utop # main ();;
Hello
{% endhighlight %}

In OCaml, `()` has type `unit`. We can see that the type of `main` is simply `unit -> unit`, because the function `print_endline` has a type of `string -> unit`, and yes, `print_endline` is an impure function. Obviously, the "pure" thing must have something to do with the `IO` typeclass.

We can say `IO ()` is an I/O action, so when we call `main ()` in Haskell, we create an I/O action. The side-effect happens when the action is actually applied. Similarly, let's see the type of `putStrLn`:

{% highlight haskell %}
Prelude> :t putStrLn
putStrLn :: String -> IO ()
{% endhighlight %}

The `putStrLn` function accept a value of type `String`, and produce an I/O action. We can imagine the I/O action will write the value of type `String` into standard output. 

So when we call an I/O function in Haskell, we are actually producing I/O actions, and the actions are not applied immediantelly.

Let's see a some more complex code:

{% highlight haskell %}
main :: IO ()
main = do putStr "> Hi, what's your name?\n> "
			name <- getLine
			putStrLn $ "> Nice to meet you, " ++ name ++ "! I'm Bo."
{% endhighlight %}

Here we are using do-notation, to apply multiple I/O actions. The do-notation is a syntax sugar. The above code is equivelant to:

{% highlight haskell %}
main :: IO ()
main = putStr "> Hi, what's your name?\n> "
		>>=
		\ _ -> getLine
		>>=
		\name -> putStrLn $ "> Nice to meet you, " ++ name ++ "! I'm Bo."
{% endhighlight %}

Here, three I/O actions are chained together by the operator `(>>=)`, `(>>=)` is a binary operator, if we check the type of `(>>=)` in ghci, we can see:

{% highlight haskell %}
Prelude> :t (>>=)
(>>=) :: Monad m => m a -> (a -> m b) -> m b
{% endhighlight %}

`m` is a Monad, in the above example, `m` is `IO`. Different `IO` action can produce values of different types. For example, `IO ()` produce a unit value, `IO String` produce a `String` value.

This article is not a Monad tutorial, if you are interested in Monad, I believe you can find a lot of articles about "Monad" on the internet. This artical doesn't require you having any knowlege of Monad or category theory, if you know what Monad is, it's good, if you don't, it totally fine.

The first argument of `(>>=)` is `IO a`, which is an I/O action produce a value of type `a`. The second argument of `(>>=)` is `a -> IO b`, which is a function. This function accept an argument of type `a`, and return an I/O action, which produce a value of type `b`.

So the `(>>=)` operator will produce a new action. In expression `let a0 = a1 >>= \v -> fun(v)`, `a1` is an I/O action, `fun(v)` will produce an I/O action. `a0` is the new created I/O action. When `a0` is executed, it firstly execute the action `a1`, then the `a1` action will produce a value `v`, then this value `v` will be passed to the function `\v -> fun(v)`, finally, the action produced by `fun(v)` will be executed.

## Implement Pure I/O in OCaml 

Use the above idea, we can implement pure I/O in other functional languages, like OCaml. In the remained part of this article, I will implement pure I/O functions in OCaml. I use OCaml here because: 

1. OCaml is very simple, and easy to use.
2. OCaml's I/O functions defined in standard library are impure, so we can implement the pure I/O using those impure functions, then we know only when the impure functions are called, side-effect happens.

To define pure I/O functions, we need to define what the I/O actions are, how can we execute those actions, and how can we chain different actions.

### Output

First, let's consider the simplest situation. If we just want to print something to standard output, then the I/O action is "output", the execution of the action is just print the content, So:

{% highlight ocaml %}
type t = Output of string
let put_string s = Output(s)
let (>>=) (Output(s)) f = 
{% endhighlight %}

The problem is, how do we implement the `(>>=)` operator? We hope the type of the return value of `(>>=)` is `t`, which is `Output(some_s)`, but how do we construct `some_s` when we don't know the definition of `f`? We can't.

So the problem of the current definition of type `t` is that, the action can not be chained, because we don't have any information about how other actions will be executed. Let's see the following definition of type `t`.

{% highlight ocaml %}
type t = Return
       | Output of string * t
{% endhighlight %}
The `Output` action is different, it is consisting of a string and another action now. The new `Output` action means print the string and do some other action. Since the definition is recursive, we need a base constructor `Return`, which means do nothing, or an empty action. Then we can chain the actions :

{% highlight ocaml %}
let put_string s = Output(s, Return)

let rec (>>=) action f =
  match action with
  | Return -> f ()
  | Output(s, io) -> Output(s, io >>= f)
  
let rec run = function
  | Return -> ()
  | Output(s, io) -> let _ = print_string s in run io
  
let main = put_string "Hello, " >>= fun _ ->
           put_string "World!\n" >>= fun _ ->
           put_string "Bye~\n"
           
let _ = run main
{% endhighlight %}
We defined `run` function, which is used to execute the I/O actions. If the action the `Return`, then do nothing, if the action the `Output`, then print the string and do the next action. The structure of `main` is 

{% highlight ocaml %}
val main : t =
    Output ("Hello, ", Output ("World~\n", Output ("Bye~\n", Return)))
{% endhighlight %}
If we run the above code, we can see the result :

{% highlight ocaml %}
utop # let _ = run main;;
Hello, World~
Bye~ 
- : unit = ()
{% endhighlight %}
The results seems good, now let's think about input.

### Input

The Input is a little different from Output, because when we write something, we know what we are writing, but when we read some thing, we don't know what we will get. The only thing we know is that we will use the value we got to do some other I/O actions by chaining the `Input` action with other actions. In `Output` action, we use a string and an I/O action, so when we chaining actions, we just chain the later action with the function as the second argument of `(>>=)`. This might give us a hint. Another problem is, since we have both `Input` and `Output`, the type of result the I/O actions will produce, might be different. We may want the IO type be polymorphic.

{% highlight ocaml %}
type 'a t = Return of 'a
          | Output of string * 'a t
          | Input of (string -> 'a t)
{% endhighlight %}
We let the `Input` action holds a function here, because we don't know what we will get from input, we only know what to do next, which is the later chained actions. So how do we chain the actions?

{% highlight ocaml %}
let rec (>>=) mv f =
  match mv with
  | Return a -> f a
  | Output(s, io) -> Output(s, io >>= f)
  | Input(fn) -> Input(fun s -> fn(s) >>= f)
{% endhighlight %}
When we chain the `Input` action with other actions, we construct a new `Input` action, inside this action, we first apply the actions in the previous `Input` action, then pass to results to the later function.

The completed code:

{% highlight ocaml %}
type 'a t =
  | Return of 'a
  | Input of (string -> 'a t)
  | Output of string * 'a t

let return a = Return a

let rec (>>=) mv f =
  begin match mv with
  | Return a -> f a
  | Input(fn) -> Input(fun s -> fn(s) >>= f)
  | Output(s, io) -> Output(s, io >>= f)
  end

let rec run = function
  | Return a -> a
  | Input(fn) -> let s = read_line () in
                 let v = fn(s) in
                 run v
  | Output(s, io) -> let _ = print_string s in
                     run io

(* unility functions *)
let get_line () = Input(fun s -> return s)
let put_line s = Output(s^"\n", return ())
let put_string s = Output(s, return ())
let get_int () = Input(fun s -> return (int_of_string s))
let put_int i = Output(string_of_int i, return ())

{% endhighlight %}

We can put those code to a IO module, then we can write codes like :

{% highlight ocaml %}
let main = put_string "> Hi, what's your name?\n> " >>= fun _ ->
           get_line () >>= fun name ->
           put_line ("> Nice to meet you, " ^ name ^ "! I'm Bo.")
           
let _ = IO.run main
{% endhighlight %}
In the above code, the body of main is pure, which means no side-effect. The structure of `main` is :

{% highlight ocaml %}
val main : unit t = Output ("> Hi, what's your name?\n> ", Input <fun>)
{% endhighlight %}


## End
